#!/usr/bin/env zsh

z4÷prune-esc() {
    integer EC QEC
    0=${${ZERO:-${(%):-%x}}:A}
    [[ -f $0 ]];EC+=$?
    zi÷reset
    source "$0:h:h"/share/preamble.inc.zsh||return 1

    # Input/options
    local -A Opts;local -a opts
    zi÷parse-opts "e -esc T -no-trim s -jspace b -bold v: -var:" Opts opts "$@"||\
        {iqw "wrong option in " $0:t " func";return 7;}
    set -- "$reply[@]"
    zi÷opt-cascade Opts -e --esc -T --no-trim -s --jspace -b --bold -v --var
    zi÷reset
    local qe=$'\x1b' __VAR_NAME=${Opts[-v]:-REPLY} __REPLY
    __REPLY=$*
    if (($+Opts[-b]));then
        __REPLY=${__REPLY//(#m)$qe\[/${MATCH}1\;}
    else
        __REPLY=${__REPLY//$qe\[[0-9\;]#[mK]}
    fi
    (($+Opts[-e]))&&__REPLY=${__REPLY//[$'\001'-$'\037']/}
    (($+Opts[-T]))||__REPLY=${__REPLY//((#s)[[:space:]]##|[[:space:]]##(#e))}
    (($+Opts[-s]))&&__REPLY=${__REPLY//(#b)([[:space:]]##)/${match[1][1]}}
    : ${(P)__VAR_NAME::=$__REPLY}
    [[ $__REPLY != [[:EMINVSTR:]] ]]
}

# vim:ft=zsh
